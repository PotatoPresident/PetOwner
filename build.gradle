plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
    id ("com.wynprice.cursemaven") version "2.1.5"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "-" + project.minecraft_version + "+"
group = project.maven_group

repositories {
    jcenter()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    modApi "me.shedaniel.cloth:config-2:4.7.0-unstable", {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "me.shedaniel.cloth:config-2:4.7.0-unstable"

    modApi "me.sargunvohra.mcmods:autoconfig1u:3.2.0-unstable", {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "me.sargunvohra.mcmods:autoconfig1u:3.2.0-unstable"

    modImplementation "io.github.prospector:modmenu:1.14.6+build.31", {
        exclude group: "net.fabricmc.fabric-api"
    }

    modRuntime "curse.maven:auth-me:3055415"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    //noinspection GrDeprecatedAPIUsage
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            //noinspection GroovyAssignabilityCheck
            artifact(remapJar) {
                //noinspection GroovyAssignabilityCheck
                builtBy remapJar
            }
            //noinspection GroovyAssignabilityCheck
            artifact(sourcesJar) {
                //noinspection GroovyAssignabilityCheck
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
